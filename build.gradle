buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group 'dev.fuxing'
version '1.0.0'

ext.jUnitVersion = '5.6.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

dependencies {
    compileOnly group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.1.3.Final'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnitVersion

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.3'
    testImplementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.1.3.Final'
    testImplementation group: 'org.glassfish', name: 'javax.el', version: '3.0.1-b11'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// uploadArchives closeAndPromoteRepository
// OSSRH publication
uploadArchives {
    repositories {
        mavenDeployer {
            // POM signature
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Target repository
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }

            pom {
                artifactId = rootProject.name

                //noinspection GroovyAssignabilityCheck
                project {
                    name = rootProject.name
                    description 'Hibernate Enum Validator'
                    packaging 'jar'
                    url 'https://github.com/fuxingloh/hibernate-validator-enum'

                    scm {
                        connection 'scm:git:https://github.com/fuxingloh/hibernate-validator-enum.git'
                        developerConnection 'scm:git:git@github.com:fuxingloh/hibernate-validator-enum.git'
                        url 'https://github.com/fuxingloh/hibernate-validator-enum.git'
                    }

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url 'https://github.com/fuxingloh/hibernate-validator-enum/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'fuxing'
                            name = 'Fuxing Loh'
                            email = 'maven@fuxing.dev'
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}
